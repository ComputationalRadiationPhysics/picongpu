/**
 * Copyright 2014-2015 Rene Widera, Alexander Grund
 *
 * This file is part of PIConGPU.
 *
 * PIConGPU is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * PIConGPU is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with PIConGPU.
 * If not, see <http://www.gnu.org/licenses/>.
 */


#pragma once

#include "simulation_types.hpp"
#include "dimensions/DataSpaceOperations.hpp"
#include "math/Vector.hpp"
#include "nvidia/atomic.hpp"

namespace picongpu
{

using namespace PMacc;

/** Copy particles from big frame to PMacc frame structure
 *
 * - convert globalCellIdx to localCellIdx
 * - processed particles per block <= number of cells per superCell
 *
 * @param counter box with three integer
 * @param destBox particle box were all particles are copied to (destination)
 * @param srcFrame frame with particles (is used as source)
 * @param maxParticles number of particles in srcFrame
 * @param localDomainCellOffset offset in cells to global origin (@see wiki PIConGPU domain definitions)
 * @param cellDesc picongpu cellDescription
 */
template<class T_CounterBox, class T_DestBox, class T_SrcFrame, class T_Space, class T_CellDescription>
__global__ void copySpeciesGlobal2Local(T_CounterBox counter, T_DestBox destBox, T_SrcFrame srcFrame,
                                        const int maxParticles,
                                        T_Space localDomainCellOffset, T_CellDescription cellDesc)
{
    using namespace PMacc::particles::operations;

    typedef T_SrcFrame SrcFrameType;
    typedef typename T_DestBox::FrameType DestFrameType;
    typedef typename T_CellDescription::SuperCellSize SuperCellSize;
    const uint32_t cellsInSuperCell = PMacc::math::CT::volume<SuperCellSize>::type::value;

    typedef DestFrameType* DestFramePtr;
    __shared__ DestFramePtr destFramePtr[cellsInSuperCell];
    __shared__ int linearSuperCellIds[cellsInSuperCell];
    __shared__ int hdf5ParticleOffset;


    const int linearThreadIdx = threadIdx.x;


    const DataSpace<simDim> superCellsCount(cellDesc.getGridSuperCells() - cellDesc.getGuardingSuperCells()*2);
    if (linearThreadIdx == 0)
    {
        /* apply for work for the full block
         * counter [0] -> offset to load particles
         */
        hdf5ParticleOffset = atomicAdd(&(counter[0]), cellsInSuperCell);
    }
    destFramePtr[linearThreadIdx] = NULL;
    linearSuperCellIds[linearThreadIdx] = -1;

    __syncthreads();

    const int globalParticleId = hdf5ParticleOffset + linearThreadIdx;
    const bool hasValidParticle = globalParticleId < maxParticles;
    DataSpace<simDim> superCellIdx;
    lcellId_t lCellIdx = INV_LOC_IDX;
    int myLinearSuperCellId = -1;

    if (hasValidParticle)
    {
        DataSpace<simDim> globalCellIdx = srcFrame[globalParticleId][globalCellIdx_];
        globalCellIdx -= localDomainCellOffset;
        superCellIdx = globalCellIdx / SuperCellSize::toRT();
        myLinearSuperCellId = DataSpaceOperations<simDim>::map(superCellsCount, superCellIdx);
        linearSuperCellIds[linearThreadIdx] = myLinearSuperCellId;
        DataSpace<simDim> inSuperCell(globalCellIdx - superCellIdx * SuperCellSize::toRT());
        lCellIdx = DataSpaceOperations<simDim>::template map<SuperCellSize>(inSuperCell);
    }
    __syncthreads();

    int masterIdx = linearThreadIdx - 1;

    if (hasValidParticle)
    {
        /* search master thread index */
        while (masterIdx >= 0)
        {
            if (myLinearSuperCellId != linearSuperCellIds[masterIdx])
            {
                break;
            }
            --masterIdx;
        }
        ++masterIdx;
        /* load empty frame if thread is the master*/
        if (masterIdx == linearThreadIdx)
        {
            /* counter[2] -> number of used frames */
            nvidia::atomicAllInc(&(counter[2]));
            DestFramePtr tmpFrame = &(destBox.getEmptyFrame());
            destFramePtr[linearThreadIdx] = tmpFrame;
            destBox.setAsFirstFrame(*tmpFrame, superCellIdx + cellDesc.getGuardingSuperCells());
        }
    }
    __syncthreads();

    if (hasValidParticle)
    {
        /* copy attributes and activate particle*/
        PMACC_AUTO(parDest, (*(destFramePtr[masterIdx]))[linearThreadIdx]);
        parDest[localCellIdx_] = lCellIdx;
        parDest[multiMask_] = 1;
        PMACC_AUTO(parDestDeselect, deselect<bmpl::vector2<localCellIdx, multiMask> >(parDest));
        assign(parDestDeselect, srcFrame[globalParticleId]);
        /* counter[1] -> number of loaded particles
         * this counter is evaluated on host side
         * (check that loaded particles by this kernel == loaded particles from HDF5 file)*/
        nvidia::atomicAllInc(&(counter[1]));
    }
}

} //namespace picongpu
